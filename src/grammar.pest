WHITESPACE = _{ " " | NEWLINE }
//empty_line = _{ NEWLINE }

comment = { "<" ~ (!">" ~ ANY)* ~ ">" }

identifier = @{ (ASCII_ALPHANUMERIC | "_")+ }
integer = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
string = @{ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

function_call = { identifier ~ "(" ~ arguments? ~ ")" }

value = _{ integer | string | function_call }
assignment = { "let " ~ identifier ~ "=" ~ value }

meta_property_access = ${ identifier ~ ".$" ~ identifier }

argument = { comment? ~ (meta_property_access | function_call | identifier) }
many_arguments = _{ argument ~ ("," ~ argument)+ }
arguments = { many_arguments | argument }
function = { comment? ~ "fn " ~ identifier ~ "(" ~ arguments? ~ ")" ~ "{" ~ body ~ "}" }
body = { (!"}" ~ expression)* }

expression = { function | assignment | function_call }


file = _{
    SOI ~
    expression* ~
    EOI
}
